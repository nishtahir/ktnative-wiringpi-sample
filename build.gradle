buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:0.3.4"
    }
}

plugins {
    id 'org.hidetake.ssh' version '2.9.0'
}

group 'com.nishtahir'
version '1.0-SNAPSHOT'

apply plugin: 'konan'

ext.programName = 'ktpi'

konan.targets = ['raspberrypi']

konanArtifacts {
    interop('wiringPi') {
        includeDirs 'src/main/c_interop/wiringPi'
    }

    program(programName) {
        libraries {
            artifact 'wiringPi'
        }
        linkerOpts '-lwiringPi -Llibs/'
    }
}

task buildWithDocker(type: Exec) {
    workingDir = projectDir

    commandLine = ["${dockerCmd}",
                   "run",
                   "--rm",
                   "-v",
                   "${projectDir}:/home/gradle/project",
                   "-v",
                   "${konanCacheDir}:/home/gradle/.konan",
                   "-w",
                   "/home/gradle/project",
                   "gradle",
                   "gradle",
                   "-Dorg.gradle.daemon=false",
                   "build"]
}

remotes {
    raspberry {
        host = "${execTargetHost}"
        user = "${execTargetUser}"
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

task deploy {
    doLast {
        ssh.run {
            session(remotes.raspberry) {
                put from: "${buildDir}/konan/bin/raspberrypi/${programName}.kexe", into: "${execTargetDir}"
                execute "chmod 755 ${execTargetDir}/${programName}.kexe"
                execute "${execTargetDir}/${programName}.kexe"
            }
        }
    }
}